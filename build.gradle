buildscript {
    ext {
        dependencyManagementPluginVersion = '1.0.4.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:${dependencyManagementPluginVersion}")
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:4.8.0'
        classpath 'com.tngtech.jgiven:jgiven-gradle-plugin:0.15.3'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.9.0'
    }
}

apply plugin: 'pl.allegro.tech.build.axion-release'
scmVersion {
    tag {
        prefix = 'queue-triage'
        initialVersion = { tag, position -> '1.0.0' }
    }
    repository {
        pushTagsOnly = true
    }
}

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'java'
    apply plugin: 'idea'

    project.version = scmVersion.version

    test {
        testLogging {
            events 'failed'
            exceptionFormat 'full'
        }
    }
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

apply from: 'dependency_config.gradle'
apply from: 'project_dependencies_config.gradle'
apply from: 'application_rpm_config.gradle'
apply from: 'database_rpm_config.gradle'
